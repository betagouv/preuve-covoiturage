test_flags := "--allow-net --allow-env"

install:
  deno install --frozen=true

test:
  deno test {{test_flags}}

watch:
  deno test {{test_flags}} --watch

ci: stack_down stack_up test stack_down

[private]
stack_up:
  #!/usr/bin/env bash
  pushd ../api
  just generate_certs && \
  just generate_keys && \
  just start_e2e "s3 postgres redis mailer proxy dex" && \
  just wait "localhost" "5432" && \
  just seed && \
  just start_e2e "api" && \
  just create_bucket "local-pdc-export" && \
  just create_bucket "local-pdc-appels-de-fonds" && \
  just create_bucket "local-pdc-public" && \
  sleep 10 && \
  popd

[private]
stack_down:
  #!/usr/bin/env bash
  pushd ../api
  just stop_e2e
  popd
